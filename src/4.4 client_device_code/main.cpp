#include <Arduino.h>
#include <BLEDevice.h>
#include <BLEUtils.h>
#include <BLEScan.h>
#include <BLEAdvertisedDevice.h>

static BLEUUID serviceUUID("90e3f8e8-6a54-4287-aad5-b3eaed884cd1");
static BLEUUID charUUID("beb5483e-36e1-4688-b7f5-ea07361b26a8");

static boolean doConnect = false;
static boolean connected = false;
static boolean doScan = false;
static BLERemoteCharacteristic* pRemoteCharacteristic;
static BLEAdvertisedDevice* myDevice;

float maxDistance = 0.0;
float minDistance = 1000.0;
int receivedCount = 0;

void processData(float distance) {
   receivedCount++;
   if (distance > maxDistance) maxDistance = distance;
   if (distance < minDistance) minDistance = distance;

   Serial.print("Received distance: ");
   Serial.print(distance);
   Serial.println(" cm");
   Serial.print("Max distance: ");
   Serial.println(maxDistance);
   Serial.print("Min distance: ");
   Serial.println(minDistance);
   Serial.print("Total received: ");
   Serial.println(receivedCount);
}

static void notifyCallback(
 BLERemoteCharacteristic* pBLERemoteCharacteristic,
 uint8_t* pData,
 size_t length,
 bool isNotify) {
   float distance = atof((char*)pData);
   processData(distance);
}

class MyClientCallback : public BLEClientCallbacks {
 void onConnect(BLEClient* pclient) {}
 void onDisconnect(BLEClient* pclient) {
   connected = false;
   Serial.println("Disconnected");
 }
};

bool connectToServer() {
   Serial.print("Connecting to ");
   Serial.println(myDevice->getAddress().toString().c_str());

   BLEClient* pClient = BLEDevice::createClient();
   pClient->setClientCallbacks(new MyClientCallback());
   pClient->connect(myDevice);
   Serial.println("Connected to server");
  
   BLERemoteService* pRemoteService = pClient->getService(serviceUUID);
   if (pRemoteService == nullptr) {
     Serial.println("Service not found");
     pClient->disconnect();
     return false;
   }
  
   pRemoteCharacteristic = pRemoteService->getCharacteristic(charUUID);
   if (pRemoteCharacteristic == nullptr) {
     Serial.println("Characteristic not found");
     pClient->disconnect();
     return false;
   }
  
   if (pRemoteCharacteristic->canNotify()) {
     pRemoteCharacteristic->registerForNotify(notifyCallback);
   }
   connected = true;
   return true;
}

class MyAdvertisedDeviceCallbacks: public BLEAdvertisedDeviceCallbacks {
 void onResult(BLEAdvertisedDevice advertisedDevice) {
   if (advertisedDevice.haveServiceUUID() && advertisedDevice.isAdvertisingService(serviceUUID)) {
     BLEDevice::getScan()->stop();
     myDevice = new BLEAdvertisedDevice(advertisedDevice);
     doConnect = true;
     doScan = true;
   }
 }
};

void setup() {
 Serial.begin(115200);
 BLEDevice::init("");
 BLEScan* pBLEScan = BLEDevice::getScan();
 pBLEScan->setAdvertisedDeviceCallbacks(new MyAdvertisedDeviceCallbacks());
 pBLEScan->setActiveScan(true);
 pBLEScan->start(5, false);
}

void loop() {
 if (doConnect) {
   if (connectToServer()) {
     Serial.println("Connected to BLE Server");
   } else {
     Serial.println("Failed to connect");
   }
   doConnect = false;
 }
 if (!connected && doScan) {
   BLEDevice::getScan()->start(0);
 }
 delay(1000);
}